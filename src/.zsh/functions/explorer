#!/usr/bin/env zsh
#shellcheck shell=bash

set -e
set -u

function __explorer::ls {
    local _cwd=$1

    {
        [ "$_cwd" = / ] || echo ..
        find "$_cwd" -mindepth 1 -maxdepth 1 -type d -print0 | sort -z | xargs -0 -n1 basename
        find "$_cwd" -mindepth 1 -maxdepth 1 ! -type d -print0 | sort -z | xargs -0 -n1 basename
    } | taggo --tag '' --icon-indices 0 --icon-delimiter '  '
}

function __explorer::interface {
    local _cwd=$1

    fzf \
        --multi \
        --cycle \
        --prompt="${_cwd%/}/" \
        --preview="
            _name=\$(taggo --tag '' --icon-indices 0 --icon-delimiter '  ' --revert <<<{})
            if [ -d $_cwd/\$_name ]; then
                exa --long --classify --color=always --all --header --time-style long-iso --git $_cwd/\$_name
            else
                bat --color=always --style=numbers --line-range :$(( LINES - 2)) $_cwd/\$_name
            fi
        " \
        --expect=enter,ctrl-g,ctrl-d
}

function __explorer::normalize_path {
    local _cwd=$1/
    local _n

    if [ "$PWD" = / ]; then
        _n=1
    else
        _n=$(( ${#PWD} + 1 ))
    fi

    xargs -I{} printf '%s%s\n' "${_cwd:$_n}" {}
}

function __explorer::select {
    local _cwd=$1

    LBUFFER+="$(__explorer::normalize_path "$_cwd" | tr '\n' ' ' | sed -E 's/ $//')"
    zle reset-prompt
    CURSOR=$#BUFFER
}

function __explorer::cd {
    local _cwd=$1
    local _name

    read -r _name

    if [ "$_name" = .. ]; then
        dirname "$_cwd"
    elif [ -d "$_cwd/$_name" ]; then
        echo "$_cwd/$_name"
    else
        echo "$_cwd"
    fi
}

function __explorer::rm {
    local _cwd=$1

    BUFFER=" rm $(__explorer::normalize_path "$_cwd" | tr '\n' ' ' | sed -E 's/ $//')"
    zle reset-prompt
    CURSOR=$#BUFFER
}

function __explorer::main {
    local _cwd=$PWD
    local _key

    while :
    do
    {
        IFS= read -r _key || return 0

        case "$_key" in
            enter)
                __explorer::select "$_cwd"
                return
                ;;

            ctrl-g)
                _cwd=$(__explorer::cd "$_cwd")
                ;;

            ctrl-d)
                __explorer::rm "$_cwd"
                return
                ;;
        esac < <(taggo --tag '' --icon-indices 0 --icon-delimiter '  ' --revert)
    } < <(__explorer::ls "$_cwd" | __explorer::interface "$_cwd")
    done
}

__explorer::main
